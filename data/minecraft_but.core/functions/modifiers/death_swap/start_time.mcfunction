#> minecraft_but.core:modifiers/death_swap/start_time
# Start death swap interval
#
# @within minecraft_but.core:modifiers/death_swap/main
# @context root
# @input
#   score death_swap.interval minecraft_but.config
#       How often to swap players, incrementing in ten seconds, default 30 (300 seconds or 5 minutes)
#   score death_swap.interval_min minecraft_but.config
#       Minimum number to randomly offset interval by 10 seconds, usually a negative number, default 3 (30 seconds)
#   score death_swap.interval_max minecraft_but.config
#       Maximum number to randomly offset interval by 10 seconds, should be positive, default 3 (30 seconds)
#   score $out random
#       Random number generated by LCG between in and in1
# @output
#   score $time minecraft_but.config
#       The time until players are swapped. Set by interval config plus random offset.
#   score $interval minecraft_but.config
#       The time until players are swapped without random offset.
#   score $random minecraft_but.death_swap
#       Random offset added to $time to make a random yet predictable offset.
#   score $min random
#       Minimum number for LCG random number
#   score $max random
#       Maximum number for LCG random number

#> Get interval
# Convert interval config into $interval
scoreboard players operation $interval minecraft_but.death_swap = death_swap.interval minecraft_but.config
# Multiply interval by 200 (10 * 20, 10 for ten seconds, 20 for converting to ticks)
scoreboard players operation $interval minecraft_but.death_swap *= #200 minecraft_but.math

#> Get random
# Get min from config
scoreboard players operation $min minecraft_but.death_swap = death_swap.interval_min minecraft_but.config
# Multiply by 200 (10 * 20, 10 for ten seconds, 20 for converting to ticks)
scoreboard players operation $min minecraft_but.death_swap *= #200 minecraft_but.math

# Get max from config
scoreboard players operation $max minecraft_but.death_swap = death_swap.interval_max minecraft_but.config
# Multiply by 200 (10 * 20, 10 for ten seconds, 20 for converting to ticks)
scoreboard players operation $max minecraft_but.death_swap *= #200 minecraft_but.math

# Set min and max to rng scores
scoreboard players operation $min random = $min minecraft_but.death_swap
scoreboard players operation $max random = $max minecraft_but.death_swap

# Get random number between min and max
function random:uniform
# Set random number to $random
#tellraw @a {"score":{"objective": "minecraft_but.rng.math","name": "out"}}
scoreboard players operation $random minecraft_but.death_swap = $out random

# Set $time to $interval
scoreboard players operation $time minecraft_but.death_swap = $interval minecraft_but.death_swap

#> Offset interval by random
scoreboard players operation $time minecraft_but.death_swap += $random minecraft_but.death_swap